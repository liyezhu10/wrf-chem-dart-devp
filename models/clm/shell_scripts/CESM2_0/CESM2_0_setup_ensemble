#!/bin/csh
#
#
# provided by UCAR, "as is", without charge, subject to all terms of use at
# http://www.image.ucar.edu/DAReS/DART/DART_download
#
# DART $Id$

#-----------------------------------------------------------------------
# Purpose, describe things here, etc.
#-----------------------------------------------------------------------

# ==============================================================================
# Options defining the experiment:
#
# CASE          The value of "CASE" will be used many ways; directory and file
#               names both locally and (possibly) on the HPSS, and script names;
#               so consider its length and information content.
# compset       Defines the vertical resolution and physics packages to be used.
#               Must be a standard CESM compset; see the CESM documentation.
# resolution    Defines the horizontal resolution and dynamics; see CESM docs.
# cesmtag       The version of the CESM source code to use when building the code.
# num_instances The number of ensemble members.
# ==============================================================================

setenv CASE           clm5_startup_2
setenv resolution     f09_g16
setenv compset        I2000Clm50BgcCrop
setenv compset        2000_DATM%GSWP3v1_CLM50%BGC-CROP_SICE_SOCN_MOSART_SGLC_SWAV
setenv cesmtag        clm_dev_branch
setenv num_instances  4

# ==============================================================================
# Directories:
# cesmdata        Location of some supporting CESM data files.
# cesmroot        Location of the CESM code base.  This version of the script
#                 only supports version cesm1_5_beta06c.
# caseroot        Defines the CESM case directory - where the CESM+DART
#                 configuration files will be stored.  This should probably not
#                 be in scratch (on yellowstone, your 'work' partition is suggested).
#                 This script will delete any existing caseroot, so this script,
#                 and other useful things should be kept elsewhere.
# rundir          Defines the location of the CESM run directory.  Will need large
#                 amounts of disk space, generally on a scratch partition.
# exeroot         Defines the location of the CESM executable directory , where the
#                 CESM executables will be built.  Medium amount of space
#                 needed, generally on a scratch partition.
# archdir         Defines the location of the CESM short-term archive directories.
#                 Requires large amounts of disk space, may be on a scratch partition if
#                 the long-term archiver is invoked to move these files to permanent storage.
#                 Files will remain here until the long-term archiver moves it to permanent storage.
# dartroot        Location of the root of _your_ DART installation
# baseobsdir      Part of the directory name containing the obs_seq.out files to be used in the
#                 assimilation. The year, month, and filename will be provided in assimilate.csh.
#                 Will be inherited by CESM#_#_DART_config and inserted into assimilate.csh
# ==============================================================================

setenv project      P86850054
setenv machine      cheyenne
setenv cesmdata     /glade/p/cesm/cseg/inputdata
setenv cesmroot     /glade/p/work/${USER}/CESM/$cesmtag
setenv caseroot     /glade/p/work/${USER}/cases/$cesmtag/${CASE}
setenv rundir       /glade/scratch/${USER}/$cesmtag/${CASE}/run
setenv exeroot      /glade/scratch/${USER}/$cesmtag/${CASE}/bld
setenv archdir      /glade/scratch/${USER}/$cesmtag/${CASE}/archive

setenv dartroot     /glade/p/work/thoar/DART/cesm_clm
setenv baseobsdir   /glade/p/image/Observations/land/SMAP

# ==============================================================================
# job settings:
#
# queue      can be changed during a series by changing the case.run
#            Cheyenne has limited queues in the early months:
#               capability (> 1152 NODES), regular (<= 1152 nodes), share (< 16 PROCS).
# timewall   can be changed during a series by changing the case.run
#
# TJH: Advancing 30 instances for 6 hours and assimilating took
#      less than 10 minutes on yellowstone using 1800 pes (120 nodes)
# ==============================================================================

setenv queue        economy
setenv timewall     0:20
setenv short_term_archiver off

# ==============================================================================
# standard commands:
#
# If you are running on a machine where the standard commands are not in the
# expected location, add a case for them below.
# ==============================================================================

set nonomatch       # suppress "rm" warnings if wildcard does not match anything

# The FORCE options are not optional.
# The VERBOSE options are useful for debugging though
# some systems don't like the -v option to any of the following
switch ("`hostname`")
   case ys*:
         # NCAR "yellowstone"
         set   MOVE = '/bin/mv -v'
         set   COPY = '/bin/cp -v --preserve=timestamps'
         set   LINK = '/bin/ln -vs'
         set REMOVE = '/bin/rm -rf'
      breaksw
   default:
         # NERSC "hopper", TACC "stampede" ... many more
         set   MOVE = 'mv -v'
         set   COPY = 'cp -v --preserve=timestamps'
         set   LINK = 'ln -vs'
         set REMOVE = 'rm -rf'
      breaksw
endsw

# FATAL idea to make caseroot the same dir as where this setup script is
# since the build process removes all files in the caseroot dir before
# populating it.  try to prevent shooting yourself in the foot.

if ( ${caseroot} == `pwd` ) then
   echo "ERROR: the setup script should not be located in the caseroot"
   echo "directory, because all files in the caseroot dir will be removed"
   echo "before creating the new case.  move the script to a safer place."
   exit 1
endif

echo "removing old files from ${caseroot}"
echo "removing old files from ${exeroot}"
echo "removing old files from ${rundir}"

${REMOVE} ${caseroot}
${REMOVE} ${exeroot}
${REMOVE} ${rundir}
${cesmroot}/cime/scripts/create_newcase  --res  ${resolution} \
                                         --mach ${machine} \
                                         --compset ${compset} \
                                         --case ${caseroot} \
                                         --project ${project} \
                                         --run-unsupported \
                                         --ninst ${num_instances} \
                                         --multi-driver || exit 2

# ==============================================================================
# Preserve a copy of this script as it was run.
# Copy the DART setup script (CESM_DART_config) to CASEROOT.
# Since we know the DARTROOT and BASEOBSDIR now, record them into
# CASEROOT/CESM_DART_config now.
# ==============================================================================

set ThisFileName = $0:t
${COPY} $ThisFileName ${caseroot}/${ThisFileName}.original

if (   -e ${dartroot}/models/clm/shell_scripts/CESM2_0/CESM2_0_DART_config ) then
   sed -e "s#BOGUS_DART_ROOT_STRING#${dartroot}#" \
       -e "s#BOGUS_DART_OBS_STRING#${baseobsdir}#" \
       -e "s#BOGUS_CASEROOT_STRING#${caseroot}#" \
          ${dartroot}/models/clm/shell_scripts/CESM2_0/CESM2_0_DART_config \
           >! ${caseroot}/CESM_DART_config  || exit 3
   chmod 755  ${caseroot}/CESM_DART_config
else
   echo "ERROR: the script to configure for data assimilation is not available."
   echo "       ${dartroot}/models/clm/shell_scripts/CESM2_0/CESM2_0_DART_config MUST exist."
   exit 4
endif

# ==============================================================================
cd ${caseroot}
# ==============================================================================

# Save a copy for debug purposes
foreach FILE ( *xml )
   if ( ! -e        ${FILE}.original ) then
      ${COPY} $FILE ${FILE}.original
   endif
end

# Get a bunch of environment variables.
# If any of these are changed by xmlchange calls in this program,
# then they must be explicty changed with setenv calls too.
#
setenv TEST_MPI              `./xmlquery MPI_RUN_COMMAND       --value`
setenv CLM_CONFIG_OPTS       `./xmlquery CLM_CONFIG_OPTS       --value`
setenv COMPSET               `./xmlquery COMPSET               --value`
setenv COMP_ATM              `./xmlquery COMP_ATM              --value`
setenv CIMEROOT              `./xmlquery CIMEROOT              --value`
setenv CASEROOT              `./xmlquery CASEROOT              --value`
setenv MAX_MPITASKS_PER_NODE `./xmlquery MAX_MPITASKS_PER_NODE --value`

# Make sure the case is configured with a data ocean.

if ( ${COMP_ATM} != datm ) then
   echo " "
   echo "ERROR: This setup script is not appropriate for active atmospheric compsets."
   echo "ERROR: Please use the models/CESM/shell_scripts/CESM2_0 examples for that case."
   echo " "
   exit 5
endif

#> @TODO stream template files & multiple years. Do we need to specify
#> year 1 and year N (performance penalty?). Can we change years on-the-fly
#> during a run

setenv resubmit            0
setenv stop_option         nhours
setenv stop_n              24

@ clm_dtime = 1800
@ h1nsteps = $stop_n * 3600 / $clm_dtime

set stream_year_align = 1998
set stream_year_first = 1998
set stream_year_last  = 2010

./xmlchange CALENDAR=GREGORIAN
./xmlchange EXEROOT="$exeroot"
./xmlchange RUNDIR="$rundir"

./xmlchange STOP_OPTION=ndays
./xmlchange STOP_N=1
./xmlchange RESUBMIT=0

./xmlchange REST_OPTION=ndays
./xmlchange REST_N=1

./xmlchange DATM_MODE=CPLHISTForcing
./xmlchange DATM_CPLHIST_YR_START=$stream_year_first
./xmlchange DATM_CPLHIST_YR_END=$stream_year_last
./xmlchange DATM_CPLHIST_YR_ALIGN=$stream_year_align


#      --- In a hybrid run the model is initialized as a startup, BUT uses
#      initialization datasets FROM A PREVIOUS case.  This
#      is somewhat analogous to a branch run with relaxed restart
#      constraints.  A hybrid run allows users to bring together combinations
#      of initial/restart files from a previous case (specified by
#      RUN_REFCASE) at a given model output date (specified by
#      RUN_REFDATE). Unlike a branch run, the starting date of a hybrid run
#      (specified by RUN_STARTDATE) can be modified relative to the reference
#      case. In a hybrid run, the model does not continue in a bit-for-bit
#      fashion with respect to the reference case. The resulting climate,
#      however, should be continuous provided that no model source code or
#      namelists are changed in the hybrid run.  In a hybrid initialization,
#      the ocean model does not start until the second ocean coupling
#      (normally the second day), and the coupler does a cold start without
#      a restart file.</desc>

setenv refcase cice5_sp2000_ens30
setenv refdate 1998-01-01
setenv startdate 1998-01-01

./xmlchange RUN_TYPE=startup
./xmlchange RUN_REFCASE=${refcase}
./xmlchange RUN_REFDATE=${refdate}
./xmlchange RUN_STARTDATE=${startdate}

# pnetcdf is default
./xmlchange PIO_TYPENAME=netcdf

# MAX_MPITASKS_PER_NODE comes from $case/Tools/mkbatch.$machine

@ ptile = $MAX_MPITASKS_PER_NODE / 2
./xmlchange MAX_TASKS_PER_NODE=$ptile
./xmlchange MAX_MPITASKS_PER_NODE=$ptile

@ nthreads = 1

echo "ptile is $ptile"

# Task layout:
# Set the nodes_per_instance below to match your case.
# By computing task counts like we do below, we guarantee each instance uses
# a whole number of nodes which is the recommended configuration.

@ nodes_per_instance = 4
@ ptile = -1

@ atm_tasks = $ptile * $nodes_per_instance
@ cpl_tasks = $ptile * $nodes_per_instance
@ ocn_tasks = $ptile * $nodes_per_instance
@ wav_tasks = $ptile * $nodes_per_instance
@ glc_tasks = $ptile * $nodes_per_instance
@ ice_tasks = $ptile * $nodes_per_instance
@ rof_tasks = $ptile * $nodes_per_instance
@ lnd_tasks = $ptile * $nodes_per_instance
@ esp_tasks = $ptile * $nodes_per_instance

./xmlchange ROOTPE_ATM=0,NTHRDS_ATM=$nthreads,NTASKS_ATM=$atm_tasks
./xmlchange ROOTPE_CPL=0,NTHRDS_CPL=$nthreads,NTASKS_CPL=$cpl_tasks
./xmlchange ROOTPE_OCN=0,NTHRDS_OCN=$nthreads,NTASKS_OCN=$ocn_tasks
./xmlchange ROOTPE_WAV=0,NTHRDS_WAV=$nthreads,NTASKS_WAV=$wav_tasks
./xmlchange ROOTPE_GLC=0,NTHRDS_GLC=$nthreads,NTASKS_GLC=$glc_tasks
./xmlchange ROOTPE_ICE=0,NTHRDS_ICE=$nthreads,NTASKS_ICE=$ice_tasks
./xmlchange ROOTPE_ROF=0,NTHRDS_ROF=$nthreads,NTASKS_ROF=$rof_tasks
./xmlchange ROOTPE_LND=0,NTHRDS_LND=$nthreads,NTASKS_LND=$lnd_tasks
./xmlchange ROOTPE_ESP=0,NTHRDS_ESP=$nthreads,NTASKS_ESP=$esp_tasks

./case.setup || exit 6

echo "case setup finished"

# ====================================

./xmlchange --subgroup case.run        --id JOB_QUEUE          --val ${queue}
./xmlchange --subgroup case.run        --id JOB_WALLCLOCK_TIME --val ${timewall}
./xmlchange --subgroup case.st_archive --id JOB_WALLCLOCK_TIME --val 1:00

# These are archiving options that may be used.
# You can turn the short/long term archivers on or off ({short,long}_term_archiver),
# but these settings should be made in either event.

if ($short_term_archiver == 'off') then
   ./xmlchange DOUT_S=FALSE
else
   ./xmlchange DOUT_S=TRUE
endif

# DEBUG = TRUE implies turning on run and compile time debugging.
# INFO_DBUG level of debug output, 0=minimum, 1=normal, 2=more, 3=too much.
./xmlchange DEBUG=TRUE
./xmlchange INFO_DBUG=1

@ inst = 1
while ( $inst <= $num_instances )

    set inst_string = `printf %04d $inst`

    # ===========================================================================
    set fname = "user_nl_datm"_${inst_string}
    # ===========================================================================
    # DATM namelist

    set FILE1 = datm.streams.txt.CPLHISTForcing.Solar_$inst_string
    set FILE2 = datm.streams.txt.CPLHISTForcing.nonSolarFlux_$inst_string
    set FILE3 = datm.streams.txt.CPLHISTForcing.State1hr_$inst_string
    set FILE4 = datm.streams.txt.CPLHISTForcing.State3hr_$inst_string

    echo "streams = '$FILE1 $stream_year_align $stream_year_first $stream_year_last'," >> ${fname}
    echo "          '$FILE2 $stream_year_align $stream_year_first $stream_year_last'," >> ${fname}
    echo "          '$FILE3 $stream_year_align $stream_year_first $stream_year_last'," >> ${fname}
    echo "          '$FILE4 $stream_year_align $stream_year_first $stream_year_last'," >> ${fname}
    echo "          'datm.streams.txt.presaero.clim_2000_$inst_string 1 1 1'"          >> ${fname}
    echo "vectors  = 'u:v' "     >> ${fname}
    echo "mapmask  = 'nomask', " >> ${fname}
    echo "           'nomask', " >> ${fname}
    echo "           'nomask', " >> ${fname}
    echo "           'nomask'  " >> ${fname}
    echo "tintalgo = 'coszen', " >> ${fname}
    echo "           'nearest'," >> ${fname}
    echo "           'linear', " >> ${fname}
    echo "           'linear'  " >> ${fname}

    # Create stream files for each ensemble member
    set SOURCEDIR = ${dartroot}/models/clm/shell_scripts/CESM2_0
    ${COPY} ${SOURCEDIR}/datm.streams.txt.CPLHISTForcing.Solar_complete        user_${FILE1}
    ${COPY} ${SOURCEDIR}/datm.streams.txt.CPLHISTForcing.nonSolarFlux_complete user_${FILE2}
    ${COPY} ${SOURCEDIR}/datm.streams.txt.CPLHISTForcing.State1hr_complete     user_${FILE3}
    ${COPY} ${SOURCEDIR}/datm.streams.txt.CPLHISTForcing.State3hr_complete     user_${FILE4}

    foreach FNAME ( user_datm.streams.txt*_${inst_string} )
       echo "modifying $FNAME"
       sed s/NINST/${inst_string}/g $FNAME >! temp
       sed s/RUNYEAR/${stream_year_first}/g temp >! $FNAME
    end
    ${REMOVE} temp

   # ===========================================================================
   set fname = "user_nl_clm"_${inst_string}
   # ===========================================================================

   echo "dtime             = $clm_dtime"              >> ${fname}
#  echo "hist_empty_htapes = .false."                 >> ${fname}
#  echo "hist_fincl1 = 'NEP'"                         >> ${fname}
#  echo "hist_fincl2 = 'NEP','FSH','EFLX_LH_TOT_R'"   >> ${fname}
#  echo "hist_fincl3 = 'TV','TLAI','PBOT','TBOT','TSA','RH2M','SNOWDP'" >> ${fname}
#  echo "hist_nhtfrq = -$stop_n,1,-$stop_n"           >> ${fname}
#  echo "hist_mfilt  = 1,$h1nsteps,1"                 >> ${fname}
#  echo "hist_avgflag_pertape = 'A','A','I'"          >> ${fname}
#  echo "hist_dov2xy = .true., .true., .false."       >> ${fname}
#  echo "hist_type1d_pertape = ' ', ' ', ' '"         >> ${fname}

   echo "hist_empty_htapes = .false."     >> ${fname}
   echo "hist_fincl1 = 'NEP'"             >> ${fname}
   echo "hist_nhtfrq = -$stop_n"          >> ${fname}
   echo "hist_mfilt  = 1"                 >> ${fname}
   echo "hist_avgflag_pertape = 'A'"      >> ${fname}
   echo "hist_dov2xy = .true."            >> ${fname}
   echo "hist_type1d_pertape = ' '"       >> ${fname}

    @ inst = $inst + 1
end

./preview_namelists

#===============================================================================
# Copy modified CLM codes into SourceMods
# ==============================================================================

#${COPY} $dartroot/models/cice/SourceMods/ice_restart_driver.F90 $caseroot/SourceMods/src.cice/
#${COPY} /glade/p/work/yfzhang/cesmcases/cesm2_0_beta05/seq_rest_mod.F90 $caseroot/SourceMods/src.drv/
#${COPY} /glade/scratch/yfzhang/inputdata_cam/ice/cice/$startdate/* $rundir/

# ./case.build || exit 7

# ==============================================================================
# What to do next
# ==============================================================================

cat << EndOfText >! CESM_instructions.txt

-------------------------------------------------------------------------
Time to check the case.

1) cd ${rundir}
   and check the compatibility between the namelists/pointer files
   and the files that were staged.

2) cd ${caseroot}

3) check things

4) run a single day, verify that it works without assimilation
   ./case.submit

5) IF NEEDED, compile all the DART executables by
   cd  ${dartroot}/models/clm/work
   ./quickbuild.csh -mpi

5) Modify the case to enable data assimilation and
   run DART by executing
   cd ${caseroot}
   ./CESM_DART_config

6) Make sure the DART-related parts are appropriate.
   Check the input.nml
   Check the assimilate.csh or perfect_model.csh - as appropriate
   ./case.submit

7) If that works
   ./xmlchange CONTINUE_RUN=TRUE
   that sort of thing
-------------------------------------------------------------------------

EndOfText

cat CESM_instructions.txt

exit 0

# <next few lines under version control, do not edit>
# $URL$
# $Revision$
# $Date$
